#version 300 es

// This is a fragment shader. If you've opened this file first, please
// open and read lambert.vert.glsl before reading on.
// Unlike the vertex shader, the fragment shader actually does compute
// the shading of geometry. For every pixel in your program's output
// screen, the fragment shader is run for every bit of geometry that
// particular pixel overlaps. By implicitly interpolating the position
// data passed into the fragment shader by the vertex shader, the fragment shader
// can compute what color to apply to its pixel based on things like vertex
// position, light position, and vertex color.
precision highp float;

uniform vec3 u_LookDirection;
uniform float u_Time;
uniform float u_FireSpeed;
uniform float u_PerlinNoiseScale;
uniform vec3 u_HotColor;
uniform vec3 u_ColdColor;

// These are the interpolated values out of the rasterizer, so you can't know
// their specific values without knowing the vertices that contributed to them
in vec4 fs_Nor;
in vec4 fs_Pos;

out vec4 out_Col; // This is the final output color that you will see on your
                  // screen for the pixel that is currently being processed.

//	Classic Perlin 3D Noise
//	by Stefan Gustavson (https://github.com/stegu/webgl-noise)
//
vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}
vec3 fade(vec3 t) {return t*t*t*(t*(t*6.0-15.0)+10.0);}

float cnoise(vec3 P, float scale){
    P *= scale;

    vec3 Pi0 = floor(P); // Integer part for indexing
    vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
    Pi0 = mod(Pi0, 289.0);
    Pi1 = mod(Pi1, 289.0);
    vec3 Pf0 = fract(P); // Fractional part for interpolation
    vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
    vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
    vec4 iy = vec4(Pi0.yy, Pi1.yy);
    vec4 iz0 = Pi0.zzzz;
    vec4 iz1 = Pi1.zzzz;

    vec4 ixy = permute(permute(ix) + iy);
    vec4 ixy0 = permute(ixy + iz0);
    vec4 ixy1 = permute(ixy + iz1);

    vec4 gx0 = ixy0 / 7.0;
    vec4 gy0 = fract(floor(gx0) / 7.0) - 0.5;
    gx0 = fract(gx0);
    vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
    vec4 sz0 = step(gz0, vec4(0.0));
    gx0 -= sz0 * (step(0.0, gx0) - 0.5);
    gy0 -= sz0 * (step(0.0, gy0) - 0.5);

    vec4 gx1 = ixy1 / 7.0;
    vec4 gy1 = fract(floor(gx1) / 7.0) - 0.5;
    gx1 = fract(gx1);
    vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
    vec4 sz1 = step(gz1, vec4(0.0));
    gx1 -= sz1 * (step(0.0, gx1) - 0.5);
    gy1 -= sz1 * (step(0.0, gy1) - 0.5);

    vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
    vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
    vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
    vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
    vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
    vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
    vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
    vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

    vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
    g000 *= norm0.x;
    g010 *= norm0.y;
    g100 *= norm0.z;
    g110 *= norm0.w;
    vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
    g001 *= norm1.x;
    g011 *= norm1.y;
    g101 *= norm1.z;
    g111 *= norm1.w;

    float n000 = dot(g000, Pf0);
    float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
    float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
    float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
    float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
    float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
    float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
    float n111 = dot(g111, Pf1);

    vec3 fade_xyz = fade(Pf0);
    vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
    vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
    float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
    return 2.2 * n_xyz;
}

float bias(float b, float t) {
    return pow(t, log(b) / log(0.5));
}

void main()
{
    float maxHeight = 4.5; // top vert starts at 1.0 and then can be distorted by as much as 2.5 upwards
    float posYNormalized = (1.0 + fs_Pos.y) / (1.0 + maxHeight);
    float alpha = 1.0;
    float intensity = 1.0;
    float radius = length(fs_Pos.xyz);
    float noise = clamp(cnoise(vec3(fs_Pos.x, fs_Pos.y - (u_FireSpeed * u_Time / 2000.0), fs_Pos.z), u_PerlinNoiseScale), 0.0, 1.0);

    vec3 emissionColor = vec3(0.8);

    // Lambertian reflection
    intensity = max(0.0, dot(fs_Nor.xyz, -u_LookDirection));

    if (u_Time > 18000.0) {
        // Mix color between firey yellow and firey red based on noise
        emissionColor = mix(emissionColor, mix(u_HotColor, u_ColdColor, noise), smoothstep(u_Time, 18000.0, 19000.0));
    }

    // Since noise controls how red or yellow the flame is, doing this means
    // that the flame will be less intense when it is more red.
    // Make flame redder, less intense, towards the top
    if (u_Time > 22000.0) {
        intensity = mix(intensity, mix(1.0, 0.5, pow(noise, 2.0)) * (1.0 - bias(0.2,  posYNormalized)), smoothstep(u_Time, 22000.0, 23000.0));
        emissionColor = mix(emissionColor, mix(emissionColor, u_ColdColor , posYNormalized), smoothstep(u_Time, 22000.0, 23000.0));
    }

    // // Note that the normals here are the normals of the original sphere. I just found this worked better even though it's a proxy.
    // // abs() because fire transmits light so it doesn't really matter if a point is facing towards or away from the camera.

    if (u_Time > 24000.0) {
        float normalAlignment = abs(dot(fs_Nor.xyz, u_LookDirection));
        alpha = mix(alpha, alpha * bias(0.2, normalAlignment), smoothstep(u_Time, 24000.0, 25000.0));

        // More red around the edges
        emissionColor = mix(emissionColor, mix(u_ColdColor, emissionColor, bias(0.8, normalAlignment)), smoothstep(u_Time, 25000.0, 25000.0));
    }


    // // Compute final shaded color
    out_Col = vec4(intensity * emissionColor, alpha);
}
